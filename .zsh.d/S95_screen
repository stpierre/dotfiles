#!/bin/zsh

__RESET_SCREEN_TITLE=
__EXPLICIT_SCREEN_TITLE=

screen_title() {
    __EXPLICIT_SCREEN_TITLE=$1
    __set_screen_title "$1"
    __RESET_SCREEN_TITLE=
}

__set_screen_title() {
    # set screen title
    if [[ -n $1 ]]; then
        print -Pn "\ek$1\e\\"
    fi
}

set_screen_screen_session() {
    # set screen title to the name of the screen sub-session
    set -- $(echo "$1" | sed 's/.*[\s\/]\?screen\s\s*//')
    zparseopts -E -- S:=session r:=session d=d
    __set_screen_title "$(echo "$session" | sed -e 's/.*\s\s*\(\d+\.\)*//')"
}

set_screen_ssh_host() {
    # set screen title to the ssh destination host
    set -- $(echo "$1" | sed 's/.*[\s\/]\?ssh\s\s*//')
    host=
    while [[ -n $1 ]]; do
        host=$1
        shift
    done
    __set_screen_title "$host"
}

default_screen_title() {
    # set the screen title to an appropriate default
    title=
    if [[ $(whoami) == "root" ]]; then
        prepend_root=1
    else
        prepend_root=0
    fi

    if [[ -n $VIRTUAL_ENV ]]; then
        # set screen title to the name of the virtualenv being worked
        # on
        title="$(basename "$VIRTUAL_ENV")"
    elif [[ -n $__CURRENT_GIT_REPO && $__CURRENT_GIT_REPO != "dotfiles" ]]
    then
        title=$__CURRENT_GIT_REPO
    elif [[ -n $__CURRENT_SVN_REPO ]]; then
        title=$__CURRENT_SVN_REPO
    else
        # set screen title to hostname or username
        if [[ $prepend_root == 1 ]]; then
            title=root
            prepend_root=0
        else
            title="$__CURRENT_HOSTNAME"
        fi
    fi
    if [[ $prepend_root == 1 ]]; then
        title="root:$title"
    fi
    __set_screen_title "$title"
}

screen() {
    # some commands run within screen change the value of $COLUMNS, so
    # we save that variable and then restore it
    local oldcols=$COLUMNS
    command screen "$@"
    export COLUMNS=$oldcols
}

_zsh_preexec_update_screen_title() {
    if [[ -n $__EXPLICIT_SCREEN_TITLE ]]; then
        __set_screen_title "$__EXPLICIT_SCREEN_TITLE"
    else
        case "$1" in
            *screen*)
                set_screen_screen_session "$1"
                __RESET_SCREEN_TITLE=1
                ;;
            *workon*|*deactivate*|*cd*|*sudo*)
                __RESET_SCREEN_TITLE=1
                ;;
            *ssh*)
                set_screen_ssh_host "$1"
                __RESET_SCREEN_TITLE=1
                ;;
        esac
    fi
}

_zsh_precmd_update_screen_title() {
    if [[ $__RESET_SCREEN_TITLE == 1 ]]; then
        default_screen_title
        __RESET_SCREEN_TITLE=
    fi
}

if [[ -n $STY || $TERM[0,6] == "screen" ]]; then
    default_screen_title
    add-zsh-hook preexec _zsh_preexec_update_screen_title
    add-zsh-hook precmd _zsh_precmd_update_screen_title
fi

alias sx='screen -x'
