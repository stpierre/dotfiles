#!/bin/zsh

__RESET_SCREEN_TITLE=
__EXPLICIT_SCREEN_TITLE=

screen_title() {
    __EXPLICIT_SCREEN_TITLE=$1
    __set_screen_title "$1"
    __RESET_SCREEN_TITLE=
}

__set_screen_title() {
    # set screen title
    if [[ -n $1 ]]; then
        print -Pn "\ek$1\e\\"
    fi
}

set_screen_screen_session() {
    # set screen title to the name of the screen sub-session
    set -- $(echo "$1" | sed 's/.*\sscreen\s\s*//;')
    zparseopts -E -- S:=session r:=session d=d
    __set_screen_title "$(echo "$session" | sed -e 's/.*\s\s*\(\d+\.\)*//')"
}

default_screen_title() {
    # set the screen title to an appropriate default
    title=
    if [[ $(whoami) == "root" ]]; then
        prepend_root=1
    else
        prepend_root=0
    fi

    if [[ -n $VIRTUAL_ENV ]]; then
        # set screen title to the name of the virtualenv being worked
        # on
        title="$(basename "$VIRTUAL_ENV")"
    elif [[ -n $__CURRENT_GIT_REPO ]]; then
        title=$__CURRENT_GIT_REPO
    else
        # set screen title to hostname or username
        if [[ $prepend_root == 1 ]]; then
            title=root
            prepend_root=0
        else
            title="$__CURRENT_HOSTNAME"
        fi
    fi
    if [[ $prepend_root == 1 ]]; then
        title="root:$title"
    fi
    __set_screen_title "$title"
}

screen() {
    # some commands run within screen change the value of $COLUMNS, so
    # we save that variable and then restore it
    oldcols=$COLUMNS
    /usr/bin/screen "$@"
    export COLUMNS=$oldcols
}

zsh_preexec_update_screen_title() {
    if [[ -n $__EXPLICIT_SCREEN_TITLE ]]; then
        __set_screen_title "$__EXPLICIT_SCREEN_TITLE"
    else
        case "$1" in
            *screen*)
                set_screen_screen_session "$1"
                __RESET_SCREEN_TITLE=1
                ;;
            *workon*|*deactivate*|*cd*|*sudo*)
                __RESET_SCREEN_TITLE=1
                ;;
        esac
    fi
}

zsh_precmd_update_screen_title() {
    if [[ $__RESET_SCREEN_TITLE == 1 ]]; then
        default_screen_title
        __RESET_SCREEN_TITLE=
    fi
}

if [[ $TERM[0,6] == "screen" ]]; then
    default_screen_title
    preexec_functions[$((${#preexec_functions} + 1))]='zsh_preexec_update_screen_title'
    precmd_functions[$((${#precmd_functions} + 1))]='zsh_precmd_update_screen_title'

    if [[ $(uname -s) == 'AIX' ]]; then
        # "screen" is not a sane term type on AIX
        export TERM=xterm
    fi
fi
